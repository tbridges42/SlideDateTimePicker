apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    version = '2.1.0'
    group = 'us.bridgeses'

    defaultConfig {
        versionName = '2.1.0'
        minSdkVersion 17
        targetSdkVersion 24
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile 'com.android.support:support-v13:24.0.0'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'us.bridgeses.SlideDateTimePicker'
                url "https://github.com/tbridges42/SlideDateTimePicker"

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'tbridges42'
                        name 'Tony Bridges'
                        email 'tbridges42@gmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/tbridges42/SlideDateTimePicker'
                    developerConnection 'https://github.com/tbridges42/SlideDateTimePicker'
                    url "https://github.com/tbridges42/SlideDateTimePicker"

                }
            }
        }
    }
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
    failOnError = false
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

Properties properties = new Properties()
properties.load(project.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "us.bridgeses.SlideDateTimePicker"
        websiteUrl = 'https://github.com/tbridges42/SlideDateTimePicker'
        vcsUrl = 'https://github.com/tbridges42/SlideDateTimePicker'
        licenses = ["Apache-2.0"]
        publish = true
    }
}

